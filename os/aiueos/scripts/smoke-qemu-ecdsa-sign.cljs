#!/usr/bin/env nbb
;; Gate for the ECDSA P-256 sign object (ssh-v1.edn / ADR-0105): the object,
;; linked into the kernel, reproduces the RFC 6979 A.2.5 P-256/SHA-256 "sample"
;; signature in a boot. Green iff the serial shows AIUEOS_ECDSA_SIGN_OK.
;;
;; Build the kernel with AIUEOS_ECDSA_SIGN_KAT=1 first
;; (build-release-image.sh with that env); driven by smoke-qemu-uefi.sh, the
;; harness proven to boot fully on this machine.

(require '[clojure.string :as str])
(def fs (js/require "node:fs"))
(def path (js/require "node:path"))
(def cp (js/require "node:child_process"))

(def aiueos (.resolve path (.dirname path *file*) ".."))
(def repo (.resolve path aiueos ".." ".."))
(def out (or (.-AIUEOS_OUT js/process.env) (.join path repo "build" "aiueos")))
(def kernel (.join path out "esp" "EFI" "AIUEOS" "KERNEL.ELF"))
(def serial-log (.join path out "kernel-serial.log"))

(defn- die [& msg]
  (binding [*out* *err*] (apply println (cons "error:" msg)))
  (.exit js/process 1))

(when-not (.existsSync fs kernel)
  (die "kernel not built with the sign KAT:"
       "\nhint: AIUEOS_ECDSA_SIGN_KAT=1 sh os/aiueos/scripts/build-release-image.sh"))
(when-not (str/includes? (.toString (.readFileSync fs kernel)) "AIUEOS_ECDSA_SIGN")
  (die "the built kernel has no ECDSA sign KAT; rebuild with AIUEOS_ECDSA_SIGN_KAT=1"))

(def env (doto (js/Object.assign #js {} js/process.env)
           (aset "AIUEOS_ECDSA_SIGN_KAT" "1")
           (aset "AIUEOS_TEST_NET" "1")))
(println "AIUEOS_ECDSA_SIGN_GATE booting via smoke-qemu-uefi.sh")
(let [r (.spawnSync cp (.join path aiueos "scripts" "smoke-qemu-uefi.sh")
                    #js [] #js {:stdio "inherit" :env env})
      serial (if (.existsSync fs serial-log)
               (str/replace (.readFileSync fs serial-log "utf8") "\r" "") "")]
  (cond
    (str/includes? serial "AIUEOS_ECDSA_SIGN_OK")
    (println "AIUEOS_ECDSA_SIGN_SMOKE_OK rfc6979-a2.5 known-answer in a real kernel boot")
    (str/includes? serial "AIUEOS_ECDSA_SIGN_FAIL")
    (die "the sign object ran and produced the WRONG signature")
    :else
    (die "the ECDSA sign marker is absent; the boot did not reach it")))
