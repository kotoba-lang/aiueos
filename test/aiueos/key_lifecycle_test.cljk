(ns aiueos.key-lifecycle-test
  "The one key-lifecycle test that needs the machine: a deployment policy
  read off disk through `aiueos.launcher/load-policy`. The other five moved
  to `grant.key-lifecycle-test` with the namespace they exercise (root
  ADR-2608219500); the helpers below are duplicated rather than shared,
  because a test-only helper crossing a repository boundary would be a
  dependency edge that exists for no production reason.
"
  (:require [grant.key-lifecycle :as lifecycle]
            [aiueos.launcher :as launcher]
            [grant.signing :as signing]
            [clojure.test :refer [deftest is]])
  (:import [java.nio.charset StandardCharsets]
           [java.security Signature]))

(def now-ms 2000)

(defn- entry
  [keys status parent components & {:keys [may-delegate?]}]
  (cond-> {:public-key (lifecycle/raw-public-hex keys)
           :status status
           :not-before-ms 1000
           :expires-at-ms 10000
           :components components}
    parent (assoc :delegated-by parent)
    may-delegate? (assoc :may-delegate? true)))

(defn- bundle
  [root epoch previous keys]
  (lifecycle/sign-bundle
   {:version 1
    :epoch epoch
    :previous-digest previous
    :issued-at-ms 1000
    :expires-at-ms 9000
    :root-id :authority/root
    :keys keys}
   (.getPrivate root)))

(defn- manifest-signature [key-pair manifest]
  (let [signer (Signature/getInstance "Ed25519")]
    (.initSign signer (.getPrivate key-pair))
    (.update signer
             (.getBytes ^String (signing/signed-message manifest)
                        StandardCharsets/UTF_8))
    (signing/hex-encode (.sign signer))))

(defn- signed-manifest [signer-id key-pair]
  (let [manifest {:aiueos/component :app/payments
                  :aiueos/wasm-sha256 "deadbeef"
                  :aiueos/signer signer-id}]
    (assoc manifest :aiueos/signature
           (manifest-signature key-pair manifest))))

(defn- base-keys [root release]
  {:authority/root
   (entry root :active nil #{:*} :may-delegate? true)
   :signer/release
   (entry release :active :authority/root #{:app/payments})})

(deftest deployment-policy-loader-admits-the-signed-next-epoch
  (let [root (lifecycle/generate-key-pair)
        release (lifecycle/generate-key-pair)
        epoch1 (bundle root 1 nil (base-keys root release))
        policy-file (java.io.File/createTempFile "aiueos-key-policy-" ".edn")]
    (try
      (spit policy-file
            (pr-str
             {:aiueos/policy :production
              :aiueos/require-signed true
              :aiueos/key-lifecycle
              {:root-public-key (lifecycle/public-key-base64 root)
               :node-state (lifecycle/initial-node-state)
               :bundle epoch1
               :now-ms now-ms}}))
      (let [policy (launcher/load-policy (.getPath policy-file))]
        (is (= 1 (:aiueos.policy/key-epoch policy)))
        (is (= :verified
               (:aiueos.signing/status
                (signing/verify
                 (signed-manifest :signer/release release)
                 policy)))))
      (finally (.delete policy-file)))))

