(ns aiueos.entropy-test
  (:require [aiueos.entropy :as entropy]
            [clojure.test :refer [deftest is]]))

(defn- failure [f]
  (try (f) nil (catch Exception error (ex-data error))))

(deftest provider-attestation-is-concrete-and-non-secret
  (let [attestation (entropy/provider-attestation)]
    (is (= :os-strong (:source attestation)))
    (is (not-empty (:algorithm attestation)))
    (is (not-empty (:provider attestation)))
    (is (= #{:continuous-duplicate
             :repetition-count
             :adaptive-proportion}
           (:health-tests attestation)))
    (is (not (contains? attestation :seed)))))

(deftest healthy-samples-pass-and-full-block-duplicates-fail
  (let [state (atom {:last-digest nil :samples 0})
        sample (byte-array (range 32))]
    (is (identical? sample (entropy/assess-sample! state sample)))
    (let [data (failure #(entropy/assess-sample! state sample))]
      (is (= [:continuous-duplicate]
             (get-in data [:aiueos.entropy/health-failure :violations]))))))

(deftest gross-source-failures-are-detected-before-release
  (is (= [:stuck-source :repetition-count]
         (entropy/health-violations (byte-array 64))))
  (let [biased (byte-array
                (concat (repeat 40 7)
                        (range 216)))]
    (is (some #{:adaptive-proportion}
              (entropy/health-violations biased)))))

(deftest production-random-api-is-bounded-and-health-checked
  (let [a (entropy/random-bytes 32)
        b (entropy/random-bytes 32)]
    (is (= 32 (alength a)))
    (is (= 32 (alength b)))
    (is (not= (vec a) (vec b))))
  (doseq [bad [0 -1 (inc entropy/max-request-bytes)]]
    (is (= {:requested bad :minimum 1 :maximum entropy/max-request-bytes}
           (:aiueos.execute/entropy-denied
            (failure #(entropy/random-bytes bad)))))))
