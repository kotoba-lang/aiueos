(ns aiueos.runtime-update-test
  (:require [aiueos.runtime-update :as runtime]
            [clojure.test :refer [deftest is testing]]))

(def digest-a (apply str (repeat 64 "a")))
(def digest-b (apply str (repeat 64 "b")))
(def manifest-id (apply str (repeat 64 "c")))
(def context {:architecture "x86_64" :aiueos-abi 4
              :kototama-runtime-abi 1})
(def artifacts
  [{:kind :component :bytes 100 :sha256 digest-a
    :url "https://ipfs.kotobase.net/ipfs/bafkreicomponent"}
   {:kind :guest :bytes 200 :sha256 digest-b
    :url "https://ipfs.kotobase.net/ipfs/bafkreiguest"}])
(def manifest
  {:schema runtime/schema :manifest-id manifest-id :sequence 2
   :timestamp-ms 1000 :artifact-digests-match? true
   :signatures [{:key-id :release/a :verified? true :status-index 0}
                {:key-id :release/b :verified? true :status-index 1}]
   :target {:architecture "x86_64" :min-aiueos-abi 4
            :min-kototama-runtime-abi 1}
   :artifacts artifacts})
(def publisher-state
  {:installed-sequence 1 :now-ms 2000
   :root {:keys #{:release/a :release/b} :threshold 2}
   :revocation-bits [0 0] :root-expires-ms 100000})
(def observed {:component digest-a :guest digest-b})
(def health {:runtime-admission true :model-bind true :murakumo-result true})

(defn pull []
  (runtime/plan-pull {:context context :manifest manifest
                      :publisher-state publisher-state}))

(defn advance [step extra]
  (runtime/advance
   (merge {:context context :manifest manifest
           :admission (:admission (pull)) :step step
           :observed-digests observed :now-ms 2500
           :previous-preserved? true :rollback-window 1
           :health-signals health :probe-elapsed-ms 1}
          extra)))

(deftest runtime-update-is-blue-green-and-never-reboots-the-kernel
  (let [planned (pull)]
    (is (= :pull (:status planned)))
    (is (= :inactive-runtime (get-in planned [:staging :slot])))
    (is (= :blue-green (get-in planned [:staging :activation])))
    (is (false? (:kernel-reboot? planned))))
  (is (= [:fetched :staged :probing :committed]
         (mapv #(-> (advance % {}) :status)
               [:idle :fetched :staged :probing])))
  (is (= :switch-routing-and-drain-previous
         (:action (advance :probing {}))))
  (is (false? (:kernel-reboot? (advance :probing {})))))

(deftest runtime-failure-rolls-back-only-the-runtime
  (let [failed (advance :probing
                        {:health-signals (assoc health :model-bind false)})]
    (is (= :rolled-back (:status failed)))
    (is (= :previous-runtime (:activate failed)))
    (is (false? (:kernel-reboot? failed))))
  (testing "missing proof waits and cannot become a commit"
    (is (= :wait-for-runtime-health
           (:action (advance :probing
                             {:health-signals (dissoc health :murakumo-result)
                              :probe-elapsed-ms 1000}))))))

(deftest runtime-bytes-remain-immutable-and-exact
  (is (some #{:artifact-url}
            (runtime/manifest-errors
             context (assoc-in manifest [:artifacts 0 :url]
                               "https://example.invalid/runtime"))))
  (is (= :artifact-digest-mismatch
         (get-in (advance :idle
                          {:observed-digests (assoc observed :guest digest-a)})
                 [:verdict :aiueos.ota/reason]))))
