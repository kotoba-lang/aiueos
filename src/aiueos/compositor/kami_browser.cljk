(ns aiueos.compositor.kami-browser
  "Headless Chrome eval of the hosted kami presenter. Loaded only by
  `clojure -M:compositor kami`. Missing Chrome is leftover :unmeasured,
  not a silent pass."
  (:require [clojure.java.io :as io])
  (:import [com.microsoft.playwright Playwright]
           [com.microsoft.playwright.options WaitUntilState]
           [java.nio.file Paths]
           [java.util HashMap]))

(defn- chrome-for-testing
  []
  (io/file (System/getProperty "user.home")
           "Library/Caches/ms-playwright/chromium-1208/chrome-mac-arm64/Google Chrome for Testing.app/Contents/MacOS/Google Chrome for Testing"))

(defn- chrome-bin
  "Prefer a Chrome whose Frameworks directory exists. The ms-playwright
  chromium-1208 tree on this Mac is a stub binary without Frameworks;
  that dlopen failure is unmeasured, not a pass."
  []
  (let [cft (chrome-for-testing)
        sys (io/file "/Applications/Google Chrome.app/Contents/MacOS/Google Chrome")
        frameworks (when (.isFile cft)
                     (io/file (.getParentFile (.getParentFile cft)) "Frameworks"))]
    (cond
      (and (.isFile cft) frameworks (.isDirectory frameworks)) cft
      (.isFile sys) sys
      :else nil)))

(defn- create-playwright
  []
  (let [env (doto (HashMap.)
              (.put "PLAYWRIGHT_SKIP_BROWSER_DOWNLOAD" "1"))
        opts (doto (com.microsoft.playwright.Playwright$CreateOptions.)
               (.setEnv env))]
    (Playwright/create opts)))

(defn eval-frame
  "Navigate the compositor SPA `#desktop` and read presenter outcome.
  Returns `{:unmeasured true :reason ...}` or `{:ok true :result ...}`."
  [url]
  (let [chrome (chrome-bin)]
    (cond
      (nil? chrome)
      {:unmeasured true :reason :chrome-missing
       :tried [(.getPath (chrome-for-testing))
               "/Applications/Google Chrome.app/Contents/MacOS/Google Chrome"]}

      :else
      (let [pw (create-playwright)
            opts (doto (com.microsoft.playwright.BrowserType$LaunchOptions.)
                   (.setHeadless true)
                   (.setExecutablePath (Paths/get (.getPath chrome)
                                                  (into-array String [])))
                   (.setArgs ["--enable-webgl"
                              "--ignore-gpu-blocklist"
                              "--use-gl=angle"
                              "--use-angle=swiftshader"
                              "--enable-unsafe-swiftshader"]))]
        (try
          (let [browser (.launch (.chromium pw) opts)
                page (.newPage browser)]
            (try
              (.navigate page
                         (str url "/")
                         (doto (com.microsoft.playwright.Page$NavigateOptions.)
                           (.setWaitUntil WaitUntilState/DOMCONTENTLOADED)))
              (.evaluate page "() => { location.hash = '#desktop'; }")
              (.waitForFunction
               page
               (str "() => {"
                    "const t = (document.getElementById('kami-out') &&"
                    " document.getElementById('kami-out').textContent) || '';"
                    "return t.indexOf('admitted') >= 0"
                    " || t.indexOf('refused') >= 0"
                    " || t.indexOf('unmeasured') >= 0"
                    " || t.indexOf('clear-only') >= 0;"
                    "}"))
              (let [result (.evaluate page
                                      (str "() => {"
                                           "const out = document.getElementById('kami-out');"
                                           "const canvas = document.getElementById('kami-viewport');"
                                           "let parsed = null;"
                                           "try { parsed = JSON.parse((out && out.textContent) || ''); } catch (e) {}"
                                           "return {"
                                           "presenter: typeof window.aiueosKamiPresent,"
                                           "executor: canvas && canvas.getAttribute('data-executor'),"
                                           "backend: canvas && canvas.getAttribute('data-backend'),"
                                           "engine: parsed && parsed.engine,"
                                           "outcome: parsed && parsed.outcome,"
                                           "instances: parsed && parsed.instances,"
                                           "reason: parsed && parsed.reason"
                                           "};"
                                           "}"))]
                {:ok true :result result})
              (finally
                (.close browser))))
          (catch Throwable e
            {:unmeasured true :reason (str (or (.getMessage e) (.getClass e)))})
          (finally
            (.close pw)))))))
