(ns aiueos.session.live
  "The legs the hosted daily shell actually runs.

  Same provider and same policy as `aiueos.cloud-live`, but invoked from the
  session HTTP process when the SPA asks — which is the P1 green condition.
  `clojure -M:cloud-live check` is a different gate and does not count."
  (:require [aiueos.cloud-live :as live]
            [aiueos.provider.cloud :as provider]))

(defn- public-outcome [verdict]
  (let [outcome (live/outcome-of verdict)]
    {:ok (= :admitted outcome)
     :outcome (name outcome)
     :status (or (:aiueos.provider.cloud/status verdict)
                 (:aiueos.cloud/status verdict))
     :reason (some-> (:aiueos.cloud/reason verdict) name)
     :fault (some-> (:aiueos.provider.cloud/error verdict) name)
     :host (:aiueos.provider.cloud/peer-host verdict)
     :peer_spki (:aiueos.provider.cloud/peer-spki verdict)
     :byte_count (:aiueos.provider.cloud/byte-count verdict)
     :digest (:aiueos.cloud/digest verdict)
     :via "session-process"}))

(defn read-cid
  "GET a CID from kotobase.net through the session process."
  ([] (read-cid nil))
  ([cid]
   (let [policy (live/with-clock (live/read-policy))
         cid (or (not-empty cid) (:aiueos.cloud-live/read-cid policy))
         origin (:aiueos.cloud/storage-origin policy)
         verdict (provider/read-block! policy cid)
         pub (public-outcome verdict)]
     (assoc pub
            :leg "storage-read"
            :cid cid
            :origin origin
            :url (str origin "/ipfs/" cid)))))

(defn infer
  "Resolve murakumo-main and POST a completion through the session process.
  No model id other than the alias is sent by this shell."
  []
  (let [policy (live/with-clock (live/read-policy))
        alias (:aiueos.cloud/model-alias policy)
        resolved (provider/resolve-model! policy)
        model (:aiueos.cloud/model resolved)]
    (if (nil? model)
      {:ok false
       :outcome "unmeasured"
       :leg "inference"
       :why "the alias did not resolve, so there was no admitted endpoint to ask"
       :alias alias
       :via "session-process"}
      (let [timeout (or (:aiueos.cloud-live/inference-timeout-ms policy) 180000)
            verdict (provider/infer!
                     policy model
                     {:messages (:aiueos.cloud-live/messages policy)
                      :max-tokens (:aiueos.cloud-live/max-tokens policy)}
                     {:request-timeout-ms timeout})
            pub (public-outcome verdict)
            completion (:aiueos.cloud/completion verdict)
            snippet (when (string? completion)
                      (if (> (count completion) 240)
                        (str (subs completion 0 240) "…")
                        completion))]
        (merge pub
               {:leg "inference"
                :alias alias
                :endpoint (:endpoint model)
                :response_shape (some-> (:aiueos.cloud/response-shape verdict) name)
                :completion_chars (:aiueos.cloud/completion-chars verdict)
                :stop_reason (:aiueos.cloud/stop-reason verdict)
                :completion snippet})))))
